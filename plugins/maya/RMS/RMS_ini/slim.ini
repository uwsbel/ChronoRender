                   	
# Slim template files generated by Cutter will have a first line
# of (commented) text that tells the ::Slim::RegisterLazyTemplates
# procedure in which menu the template should be placed. A typical
# first line might be,
#          # menuInfo fundza,MyColor#0 color MyColor utility
# This line of text has the effect of telling Slim that a template
# named "MyColor" implements a color node. As a menu item it
# should be labelled "MyColor" and should appear in the "Utility"
# sub-menu of the "Colors" menu that is displayed by Slim.
#
# Author: Malcolm Kesson Jan 6 2010
# 
# This script is suitable for dual-boot Windows/Linux computers
# The following path WILL require editing

set crpath Z:/Users/Aaron/Desktop/ChronoRender/plugins/maya/RMS
set userdir $crpath/RMS_slim
  
set os [array get tcl_platform platform]
if {[llength $os] == 2} {
    set os [lindex $os 1]
    }
# Wrong guess, we're on linux...
if {$os == "unix"} {
    # The following path WILL require editing
    set userdir /PATH_TO/maya/projects/RMS_slim
    }  
  
if { [file exists $userdir] } {
    set slimfiles [glob -nocomplain -directory $userdir *slim]
    # Might be useful to log our templates as they are registered
    set logfile [open "$userdir/slim_log.txt" w]
    set day  [clock format [clock seconds] -format %D]
    set time [clock format [clock seconds] -format %R]
    puts $logfile "Registered on $day at $time"
  
    foreach item $slimfiles {       
        set slimName [file tail $item]
        puts $logfile "$slimName"
        set fileID [open $userdir/$slimName r]
  
        # Read the first line of text. It may contain Cutter's 
        # "menuInfo" tag. The tag provides a hint about how the 
        # slim template should be registered.
        gets $fileID info
        close $fileID
        
        # Remove the comment character and any leading white
        # space 
        set info [string trimleft $info "\# "]
    
        # Does the line begin with the "menuInfo" tag? The tag is
        # automatically added by Cutter when it is used to generate
        # a slim template file from a users shader source code file.
        # However, if a user has authored their own template files
        # they will probably NOT have added the "menuInfo" tag.
        if { [string equal -nocase -length 8 $info "menuInfo"] == 1} {
            set info [string trimleft $info]
            # Remove the tag
            set info [string range $info 8 end]
            set info [string trimleft $info]
            
            set rltStr     "::Slim::RegisterLazyTemplates \{\n"
            append rltStr  "    $userdir/$slimName \{\n"
            append rltStr  "        \{ $info \}\n"
            append rltStr  "    \}\n"
            append rltStr  "\}"
            eval $rltStr
        } else {
            # Cannot use lazy registration - load the template immediately.
            LoadExtension slim $item slim
            }
        }
    close $logfile
    }
